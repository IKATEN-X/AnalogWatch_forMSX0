10 COLOR 15,1,1:SCREEN 5,2
20 SET PAGE 0,3:BLOAD"wf1.sc5",S:COLOR=RESTORE
30 SP$="":FOR J=0 TO 31:READ D$:SP$=SP$+CHR$(VAL("&H"+D$)):NEXT J
40 FOR I=0 TO 1:SET PAGE 0,I:CLS:SPRITE$(0)=SP$:NEXT I
50 _TURBOON
60 '------- prepare variable
70 DIM HX(21),HY(21),HC(21),MX(21),MY(21),MC(21),X1(21),Y1(21),TM(13),PR(16),PG(16),PB(16),SX(60),SY(60)
80 RD= 3.14159265#/180:DP$="sample/analog":CX=128:CY=105
85 PS=-1:PM=-1:SR=90:P1=0:P2=1:PD$="":PN=-1:PY=-1
90 FORI=0 TO 59:DG=RD*I*6:SX(I)=SR*SIN(DG)+CX+.5:SY(I)=-SR*COS(DG)+CY+.5:NEXT I
93 SET PAGE 0,3:FOR I=0 TO 15:AD=30336+I*2:V=VPEEK(AD):PR(I)=(V AND 240)/16:PB(I)=V AND 15:PG(I)=VPEEK(AD+1):NEXT I
100 '------- read needle data
110 FOR J=0 TO 20:READ MX(J),MY(J),MC(J):NEXT J
120 FOR J=0 TO 20:READ HX(J),HY(J),HC(J):NEXT J
130 '----- main loop
140 SET PAGE P1,P1
150 OUT&HB4,13:OUT&HB5,8
160 FORI=0TO12:OUT&HB4,I:TM(I)=INP(&HB5)AND15:NEXTI:OUT&HB4,13:OUT&HB5,8
170 YY=1980+TM(12)*10+TM(11):DT$="":FOR J=10 TO 7 STEP -1:DT$=DT$+CHR$(TM(J)+48):NEXT J
180 IF PD$<>DT$ THEN PD$=DT$:GOSUB 330
185 GOSUB1000
190 IF STRIG(0) THEN END
200 IF PS=TM(0) THEN 150 ELSE PS=TM(0)
210 S=TM(1)*10+TM(0):PUT SPRITE 0,(SX(S)-6,SY(S)-6),8,0
250 IF PM=TM(2) THEN 150 ELSE PM=TM(2)
260 SETPAGE P1,P2
270 COPY(44,0)-(255,212),3 TO (23,0),P2
280 H=(TM(5)*10+TM(4))MOD12:M=TM(3)*10+TM(2)
290 D=M*6:GOSUB 440
300 D=(H+(M/60))*30:GOSUB 530
310 SWAP P1,P2
320 GOTO 140
330 '----- put date info
340 YS$=STR$(YY)
350 FOR J=1 TO 4
360 JJ=(J-1)*15:NY=VAL(MID$(YS$,J+1,1))*15
370 COPY(2,NY)-(15,NY+14),3 TO(121+JJ,88),3:NEXT J
380 FOR J=1 TO 4
390 JJ=(J-1)*15:NY=VAL(MID$(DT$,J,1))*15
400 COPY(2,NY)-(15,NY+14),3 TO(121+JJ,106),3:NEXT J
410 NY=TM(6)*9+150
420 COPY(0,NY)-(43,NY+8),3 TO(127,123),3:NEXT J
430 RETURN
440 '----- draw minute hand
450 DG=RD*D
460 FORJ=0 TO 20
470 X1(J)=MX(J)*COS(DG)-MY(J)*SIN(DG)+CX+.5
480 Y1(J)=MX(J)*SIN(DG)+MY(J)*COS(DG)+CY+.5
490 NEXT J
500 LINE(X1(0),Y1(0))-(X1(1),Y1(1)),MC(0)
510 FOR LI=1 TO 20:LINE-(X1(LI),Y1(LI)),MC(LI):NEXT LI
520 RETURN
530 '----- draw hour hand
540 DG=RD*D
550 FORJ=0 TO 20
560 X1(J)=HX(J)*COS(DG)-HY(J)*SIN(DG)+CX+.5
570 Y1(J)=HX(J)*SIN(DG)+HY(J)*COS(DG)+CY+.5
580 NEXT J
590 LINE(X1(0),Y1(0))-(X1(1),Y1(1)),HC(0)
600 FOR LI=1 TO 20:LINE-(X1(LI),Y1(LI)),HC(LI):NEXT LI
610 RETURN
1000 '---- iotget
1010 OUT8,&HAC:OUT8,1:L=LEN(DP$)
1020 OUT8,L:FORI=1TOL:OUT8,ASC(MID$(DP$,I,1)):NEXTI:OUT8,&HAB
1030 R=INP(8)+INP(8)*256:R=VAL("&h"+HEX$(R))
1040 RT=R/4095
1050 FOR I=0 TO 15:R=PR(I)*RT+.5:G=PG(I)*RT+.5:B=PB(I)*RT+.5
1060 AD=30336+I*2:VPOKE AD,((R*16)AND240)+(BAND7):VPOKE AD+1,G:NEXT I:COLOR=RESTORE
1070 RETURN
2000 '----- second hand
2010 DATA 0F,3F,7F,7F,FF,FF,FF,FF,FF,7F,7F,3F,0F,00,00,00
2020 DATA 00,C0,E0,E0,F0,F0,F0,F0,F0,E0,E0,C0,00,00,00,00
2030 '----- hour hand
2040 DATA -1,-32,7, -1,-86,7,  0,-87,7,  1,-86,7,  1,-32,7
2050 DATA -2,-32,7, -2,-86,7,  0,-88,7,  2,-86,7,  2,-32,7
2060 DATA -3,-31,7, -3,-86,7,  0,-89,7,  3,-86,7,  3,-31,7
2070 '----- minute hand
2080 DATA -4,-30,7, -4,-86,1,  0,-90,1,  4,-86,1,  4,-30,1, -4,-30,1
2090 DATA -5,-33,4, -4,-63,4, 0,-68,4, 4,-63,4, 5,-33,4
2100 DATA -6,-32,4, -5,-63,4, 0,-69,4, 5,-63,4, 6,-32,4
2110 DATA -7,-31,4, -6,-63,4, 0,-70,4, 6,-63,4, 7,-31,4
2120 DATA -8,-30,4, -7,-63,1, 0,-71,1, 7,-63,1, 8,-30,1, -8,-30,1
